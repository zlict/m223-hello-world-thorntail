buildscript {
  String thorntailVersion = System.getProperty('thorntailVersion') ?: VERSION_THORNTAIL

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }

  dependencies {
    classpath "io.thorntail:thorntail-gradle-plugin:$thorntailVersion"
  }
}

String thorntailVersion = System.getProperty('thorntailVersion') ?: VERSION_THORNTAIL

apply plugin: 'thorntail'
apply plugin: 'war'

thorntail {
  properties {
    swarm.http.host = 'localhost'
    swarm.http.port = 8181
  }
  bundleDependencies = true
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://maven.repository.redhat.com/nexus/content/repositories/thirdparty-releases/'
  }
  maven {
    url "https://repo.gradle.org/gradle/libs-releases-local"
  }
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url "http://repository.jboss.org/nexus/content/groups/public/"
  }
}

dependencies {
  compileOnly 'javax:javaee-api:8.0'
  testCompileOnly 'javax:javaee-api:8.0'

  // Include the Thorntail BOM for compilation.
  implementation(enforcedPlatform("io.thorntail:bom-all:$thorntailVersion"))
  implementation "io.thorntail:cdi"
  implementation "io.thorntail:jaxrs"

  implementation "commons-io:commons-io:2.4"

  testImplementation "junit:junit:4.12"
  testImplementation "org.gradle:gradle-tooling-api:${gradle.gradleVersion}"
  testImplementation "io.thorntail:gradle-arquillian-adapter:$thorntailVersion"
}

test {
  systemProperty 'thorntailVersion', thorntailVersion
}

task runApp(type: JavaExec, dependsOn: [assemble, 'thorntail-package']) {
  jvmArgs += '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8787'
  main = '-jar'

  doFirst() {
    File file = (project.tasks['jar'].destinationDir as File).listFiles(new FilenameFilter() {
      @Override
      boolean accept(File dir, String name) {
        return name.endsWith('-thorntail.jar')
      }
    }).first()
    args file.absolutePath
  }
}